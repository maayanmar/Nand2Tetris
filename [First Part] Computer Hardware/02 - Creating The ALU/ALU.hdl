// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // You're advised to work on the ALU chip in two steps:
    // - First, without handling status outputs (ALU-nostat)
    // - Then, adding the missing functionality for the "full" chip (ALU). 

    // Handle X:
    Mux16(a=x, sel=zx, out=X);  // b[0..15]=false automatically
    Not16(in=X, out=notX);
    Mux16(a=X, b=notX, sel=nx, out=FinalX);

    // Handle Y:
    Mux16(a=y, sel=zy, out=Y);  // b[0..15]=false automatically
    Not16(in=Y, out=notY);
    Mux16(a=Y, b=notY, sel=ny, out=FinalY);

    // Handle F:
    Add16(a=FinalX, b=FinalY, out=XplusY);
    And16(a=FinalX, b=FinalY, out=XandY);
    Mux16(a=XandY, b=XplusY, sel=f, out=FXY);

    // Handle NO:
    Not16(in=FXY, out=notFXY);
    Mux16(a=FXY, b=notFXY, sel=no, out=out, out=ZR, out[15]=ng); // ng Handled for free

    // Handle ZR:
    Not16(in=ZR, out=bus);
    AndWay16(in=bus, out=zr);
}